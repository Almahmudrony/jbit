<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" lang="en">
<head>
<title>JBit</title>
<meta http-equiv="content-type" content="text/html; charset=iso-8859-1" />
<meta name="viewport" content="width=device-width" />
<meta name="HandheldFriendly" content="true" />
<link rel="shortcut icon" href="/favicon.ico" type="image/x-icon" />
<style type="text/css">
body { font-family: sans-serif; }
</style>
<link rel="stylesheet" type="text/css" href="jbit-sim.css" />
<script type="text/javascript" src="vm.js"></script>
<script type="text/javascript" src="jbit-vm.js"></script>
<script type="text/javascript" src="jbit-microio.js"></script>
<script type="text/javascript" src="e1.js"></script>
<script type="text/javascript" src="jbit-sim.js"></script>
</head>
<body>
<div id="jb_float">
<div id="jb_top">
</div>
</div>

<h1>e1</h1>

<pre>.include &quot;jbit.inc&quot;

d_frmfps = FRMFPS
d_frmdraw = FRMDRAW
d_keybuf = KEYBUF
d_convideo = CONVIDEO
d_conrow1 = CONROW1

v_io_hi = IO_HI
v_video_lo = CONVIDEO_LO

v_asc_spc = ' '
v_asc_3 = '3'
v_asc_9 = '9'
v_chr_dot = %10000000

v_msk_am = %00000001
v_msk_pm = %00000010
v_msk_dots = %00000100

v_key_set = 1
v_key_mode = 2

v_dsp_am = vleds_table_am - vleds_table
v_dsp_pm = vleds_table_pm - vleds_table
v_dsp_dots = vleds_table_dots - vleds_table

v_vidsize = 40

v_st_start = 0
v_st_sethh = 1
v_st_setm1 = 2
v_st_setm0 = 3
v_st_hhmm = 4
v_st_ss = 5

d_tab_l_d0 = vleds_table 
d_tab_l_c0 = vleds_table+1
d_tab_l_d1 = vleds_table+2
d_tab_l_c1 = vleds_table+3

.if (level &gt;= 3)
d_tab_s_d0 = segments_table
d_tab_s_m0 = segments_table+1
d_tab_s_d1 = segments_table+2
d_tab_s_m1 = segments_table+3
.endif

z_mdl_tcks = 1
z_mdl_s0 = 2
z_mdl_s1 = 3
z_mdl_m0 = 4
z_mdl_m1 = 5
z_mdl_hh = 6

z_irq_lo = 10
z_irq_hi = 11
z_out_7s_0 = 12
z_out_7s_1 = 13
z_out_7s_2 = 14
z_out_7s_3 = 15
z_out_leds = 16
z_in_keys = 17
z_ram_0 = 18
z_wdtmr = 19
z_rom_7s_0 = 20
z_rom_7s_1 = 21
z_rom_7s_2 = 22
z_rom_7s_3 = 23
z_rom_7s_4 = 24
z_rom_7s_5 = 25
z_rom_7s_6 = 26
z_rom_7s_7 = 27
z_rom_7s_8 = 28
z_rom_7s_9 = 29
z_rom_vers = 30
z_rom_freq = 31

z_status = 18

z_shadow = 32

z_vlds_msk = 40
z_accbuf = 41 ; x6
z_vptr_lo = 47
z_vptr_hi = 48
z_mask = 49
z_curr_reg = 50
z_curr_val = 51
z_offset = 52
z_tmp_f_lo = 53
z_tmp_f_hi = 54

v_reg_leds = 4

z_test_tmp = 80

.code

.if (level = 8)

.export c_boot
c_boot:

.else

.export c_test
c_test:

.endif

.if (level = 1)

.export c_vleddraw
.export v_dsp_am
.export v_dsp_pm
.export v_dsp_dots

	ldx	#v_dsp_am
	lda	#1
	jsr	c_vleddraw
	ldx	#v_dsp_dots
	lda	#1
	jsr	c_vleddraw

.endif

.if (level = 2)

.export c_vldsdraw
.export d_frmfps
.export d_frmdraw
.export z_test_tmp
.export v_msk_am
.export v_msk_pm
.export v_msk_dots

v_two_fps = 8
.export v_two_fps

	lda	#v_two_fps
	sta	d_frmfps
	lda	#v_msk_pm
	sta	z_test_tmp
@l1:	lda	z_test_tmp
	eor	#v_msk_dots
	sta	z_test_tmp
	jsr	c_vldsdraw
	lda	#0
	sta	d_frmdraw
	jmp	@l1

.endif

.if (level = 3)

;v_ssmask_j = %01111000
;v_ssmask_b = %00111110
;v_ssmask_b = %11111110
;v_ssmask_i = %00001000
;v_ssmask_t = %00001110
;.export v_ssmask_j
;.export v_ssmask_b
;.export v_ssmask_i
;.export v_ssmask_t

v_a_msk = %11101110
v_a_dsp = 4
.export v_a_msk
.export v_a_dsp

	lda	#v_a_msk
	ldx	#v_a_dsp
	jsr	c_ssegdraw

.endif

.if (level &gt;= 4 &amp;&amp; level &lt; 8)

.export c_handler
.export c_halstart
.export z_irq_lo
.export z_irq_hi

	lda	#&lt;c_handler
	sta	z_irq_lo
	lda	#&gt;c_handler
	sta	z_irq_hi
	jmp	c_halstart
c_handler:

.endif

.if (level = 4)

	inc	CONVIDEO
	rts

.endif

.if (level = 5)

.export z_out_7s_0
.export z_out_7s_1
.export z_out_7s_2
.export z_out_7s_3
.export z_out_leds
.export z_in_keys
.export z_ram_0
.export z_wdtmr
.export z_rom_7s_0
.export z_rom_7s_1
.export z_rom_7s_2
.export z_rom_7s_3
.export z_rom_7s_4
.export z_rom_7s_5
.export z_rom_7s_6
.export z_rom_7s_7
.export z_rom_7s_8
.export z_rom_7s_9
.export z_rom_vers
.export z_rom_freq
.export v_msk_am
.export v_msk_pm
.export v_msk_dots
.export v_key_set
.export v_key_mode

	lda	#100
	sta	z_wdtmr
	ldx	z_in_keys
	beq	@ret
	lda	z_rom_7s_0,x
	sta	z_out_7s_1
@ret:	rts

.endif

.if (level = 6)

.export c_mdl_init
.export c_mdl_tick
.export c_mdl_go
.export z_mdl_s0
.export z_status
.export z_out_7s_3
.export z_rom_7s_0

	lda	z_status
	bne	@tick

@init:	jsr	c_mdl_init
	jsr	c_mdl_go
	inc	z_status
	rts

@tick:	jsr	c_mdl_tick
	ldx	z_mdl_s0
	lda	z_rom_7s_0,x
	sta	z_out_7s_3
	rts

.endif

.if (level = 7)

.export c_mdl_init
.export c_mdl_edit
.export c_upd_view
.export z_wdtmr
.export z_status
.export z_in_keys
.export v_key_set
.export v_key_mode
.export v_st_start
.export v_st_sethh
.export v_st_hhmm

	lda	#100
	sta	z_wdtmr

	lda	z_status
	beq	@init

	lda	z_in_keys
	cmp	#v_key_set
	beq	@set
	cmp	#v_key_mode
	beq	@mode
	rts

@set:	inc	z_status
	lda	z_status
	cmp	#v_st_hhmm
	beq	@j1
	jmp	c_upd_view

@mode:	jsr	c_mdl_edit
	jmp	c_upd_view

@init:	jsr	c_mdl_init
@j1:	lda	#v_st_sethh
	sta	z_status
	jmp     c_upd_view

.endif

.if (level = 8)

.export c_main

	jmp	c_main

.endif

	brk

.align	256


.if (level = 1)
.export d_tab_l_d0
.export d_tab_l_c0
.export d_tab_l_d1
.export d_tab_l_c1
.export d_convideo
.export v_asc_spc
.endif
.if (level &gt;= 1)

c_vleddraw:
	bne	@on
@off:	lda	#v_asc_spc
	ldy	d_tab_l_d0,x
	sta	d_convideo,y
	ldy	d_tab_l_d1,x
	sta	d_convideo,y
	rts
@on:	ldy	d_tab_l_d0,x
	lda	d_tab_l_c0,x
	sta	d_convideo,y
	ldy	d_tab_l_d1,x
	lda	d_tab_l_c1,x
	sta	d_convideo,y
	rts

.endif

.if (level = 2)
.export c_vldsdraw
.export c_vleddraw
.export z_vlds_msk
.export v_msk_am
.export v_msk_pm
.export v_msk_dots
.export v_dsp_am
.export v_dsp_pm
.export v_dsp_dots
.endif
.if (level &gt;= 2)

c_vldsdraw:
	sta	z_vlds_msk
	ldx	#v_dsp_am
	and	#v_msk_am
	jsr	c_vleddraw
	ldx	#v_dsp_pm
	lda	z_vlds_msk
	and	#v_msk_pm
	jsr	c_vleddraw
	ldx	#v_dsp_dots
	lda	z_vlds_msk
	and	#v_msk_dots
	jmp	c_vleddraw

.endif

.if (level = 3)
.export c_ssegdraw
.export d_tab_s_d0
.export d_tab_s_m0
.export d_tab_s_d1
.export d_tab_s_m1
.export d_tab_av_d
.export z_vptr_lo
.export z_vptr_hi
.export z_mask
.export z_accbuf
.export v_io_hi
.export v_asc_spc
.export v_chr_dot
.endif
.if (level &gt;= 3)

c_ssegdraw:
	stx	z_vptr_lo
	sta	z_mask
	lda	#0
	tay
	ldx	#6
@l1:	sta	z_accbuf-1,x
	dex
	bne	@l1
@l2:	asl	z_mask
	bcc	@next
	ldx	d_tab_s_d0,y
	lda	d_tab_s_m0,y
	ora	z_accbuf,x
	sta	z_accbuf,x
	ldx	d_tab_s_d1,y
	lda	d_tab_s_m1,y
	ora	z_accbuf,x
	sta	z_accbuf,x
@next:	iny
	iny
	iny
	iny
	cpy	#7*4
	bne	@l2
	lda	#v_io_hi
	sta	z_vptr_hi
@con:	ldx	#0
@l3:	lda	z_accbuf,x
	beq	@j1
	ora	#v_chr_dot
	bne	@j2
@j1:	lda	#v_asc_spc
@j2:	ldy	d_tab_av_d,x
	sta	(z_vptr_lo),y
	inx
	cpx	#6
	bne	@l3
	rts

.endif

.if (level = 4)
.export c_halstart
.export c_halinit
.export c_halabort
.export c_halstep
.export d_frmdraw
.export d_convideo
.export d_conrow1
.export d_regimage
.export d_errormsg
.export z_wdtmr
.export z_ram_0
.export z_rom_vers
.export z_rom_freq
.export z_shadow
.export v_asc_spc
.export v_vidsize
.endif
.if (level &gt;= 4)

c_halstart:
	jsr	c_halinit
@l1:	jsr	c_halstep
	dec	z_wdtmr
	beq	c_halabort
	lda	#0
	sta	d_frmdraw
	beq	@l1

c_halinit:
	ldx	#20
@l1:	lda	d_regimage-1,x
	sta	z_out_7s_0-1,x
	dex
	bne	@l1
	lda	#0
	ldx	#5
@l2:	sta	z_shadow-1,x
	dex
	bne	@l2
	rts

c_halabort:
	ldx	#v_vidsize
	lda	#v_asc_spc
@l1:	sta	d_convideo-1,x
	dex
	bne	@l1
@l2:	lda	d_errormsg,x
	beq	@l3
	sta	d_conrow1,x
	inx
	bne	@l2
@l3:	sta	d_frmdraw
	beq	@l3

.endif

.if (level = 4)

c_halstep:
	jmp	(z_irq_lo)

.endif

.if (level = 5)
.export d_keybuf
.export d_dsp_dgts
.export c_vldsdraw
.export c_ssegdraw
.export c_halstep
.export z_shadow
.export z_curr_reg
.export z_curr_val
.export v_key_set
.export v_key_mode
.export v_asc_3
.export v_asc_9
.export v_reg_leds
.endif
.if (level &gt;= 5)

c_halstep:
	lda	d_keybuf
	beq	@nokey
@chk3:	cmp	#v_asc_3
	bne	@chk9
	lda	#v_key_set
	bne	@putk
@chk9:	cmp	#v_asc_9
	bne	@put0
	lda	#v_key_mode
	bne	@putk
@put0:	lda	#0
@putk:	ldx	#1
	stx	d_keybuf
@nokey:	sta	z_in_keys
	jsr	@jmp
	lda	#v_reg_leds
	sta	z_curr_reg
@l1:	ldx	z_curr_reg
	lda	z_out_7s_0,x
	cmp	z_shadow,x
	beq	@j1
	sta	z_shadow,x
	sta	z_curr_val
	cpx	#v_reg_leds
	bne	@j2
	jsr	c_vldsdraw
	jmp	@j1
@j2:	lda	d_dsp_dgts,x
	tax
	lda	z_curr_val
	jsr	c_ssegdraw
@j1:	dec	z_curr_reg
	bpl	@l1
	rts
@jmp:	jmp	(z_irq_lo)

.endif

.if (level = 6)
.export c_mdl_init
.export c_mdl_tick
.export z_mdl_tcks
.export z_mdl_s0
.export z_mdl_s1
.export z_mdl_m0
.export z_mdl_m1
.export z_mdl_hh
.export z_rom_freq
.endif
.if (level &gt;= 6)

c_mdl_init:
	lda	#0
	sta	z_mdl_m0
	sta	z_mdl_m1
	sta	z_mdl_hh
	rts

c_mdl_go:
	lda	#0
	sta	z_mdl_tcks
	sta	z_mdl_s0
	sta	z_mdl_s1
	rts

c_mdl_tick:
	ldx	#0
	inc	z_mdl_tcks
	lda	z_mdl_tcks
	cmp	z_rom_freq
	bne	@ret
	stx	z_mdl_tcks
	inc	z_mdl_s0
	lda	z_mdl_s0
	cmp	#10
	bne	@ret
	stx	z_mdl_s0
	inc	z_mdl_s1
	lda	z_mdl_s1
	cmp	#6
	bne	@ret
	stx	z_mdl_s1
	inc	z_mdl_m0
	lda	z_mdl_m0
	cmp	#10
	bne	@ret
	stx	z_mdl_m0
	inc	z_mdl_m1
	lda	z_mdl_m1
	cmp	#6
	bne	@ret
	stx	z_mdl_m1
	inc	z_mdl_hh
	lda	z_mdl_hh
	cmp	#24
	bne	@ret
	stx	z_mdl_hh
@ret:	rts

.endif

.if (level = 7)
.export c_mdl_edit
.export c_upd_view
.export c_uv_sethh
.export c_uv_setm1
.export c_uv_setm0
.export c_uv_hhmm
.export c_uv_ss
.export c_uv_hhlds
.export c_uv_min1
.export c_uv_min0
.export d_tab_efld
.export d_tab_emax
.export z_mdl_tcks
.export z_mdl_s0
.export z_mdl_s1
.export z_mdl_m0
.export z_mdl_m1
.export z_mdl_hh
.export z_out_7s_0
.export z_out_7s_1
.export z_out_7s_2
.export z_out_7s_3
.export z_out_leds
.export z_status
.export z_rom_7s_0
.export z_rom_7s_1
.export z_rom_7s_2
.export z_rom_freq
.export v_st_start
.export v_st_sethh
.export v_st_setm1
.export v_st_setm0
.export v_st_hhmm
.export v_st_ss
.export v_msk_am
.export v_msk_pm
.export v_msk_dots
.endif
.if (level &gt;= 7)

c_mdl_edit:
	ldy	z_status
	dey
	lda	d_tab_efld,y
	tax
	inc	0,x
	lda	0,x
	cmp	d_tab_emax,y
	bne	@ret
	lda	#0
	sta	0,x
@ret:	rts

c_upd_view:
	ldx	#0
	lda     z_status
	cmp	#v_st_sethh
	beq	c_uv_sethh
	cmp	#v_st_setm1
	beq	c_uv_setm1
	cmp	#v_st_setm0
	beq	c_uv_setm0
	cmp	#v_st_hhmm
	beq	c_uv_hhmm
	cmp	#v_st_ss
	beq	c_uv_ss
	rts

c_uv_sethh:
	stx	z_out_7s_2
	stx	z_out_7s_3
	jmp	c_uv_hhlds

c_uv_setm1:
	stx	z_out_7s_3
	jmp	c_uv_min1

c_uv_setm0:
	jmp	c_uv_min0

c_uv_hhmm:
	jsr	c_uv_hhlds
	jsr	c_uv_min1
	jsr	c_uv_min0
	rts

c_uv_ss:
	stx	z_out_7s_0
	stx	z_out_7s_1
	stx	z_out_leds
	ldx	z_mdl_s1
	lda	z_rom_7s_0,x
	sta	z_out_7s_2
	ldx	z_mdl_s0
	lda	z_rom_7s_0,x
	sta	z_out_7s_3
	rts

c_uv_hhlds:
	stx	z_out_7s_0
	ldy	#v_msk_dots
	lda	z_status
	cmp	#v_st_hhmm
	bne	@AMPM
	lda	z_rom_freq
	lsr
	cmp	z_mdl_tcks
	bpl	@AMPM
	ldy	#0
@AMPM:	tya
	ldy	z_mdl_hh
	cpy	#12
	bmi	@AM
@PM:	ora	#v_msk_pm
	sta	z_out_leds
	lda	z_mdl_hh
	sec
	sbc	#12
	jmp	@hXX
@AM:	ora	#v_msk_am
	sta	z_out_leds
	lda	z_mdl_hh
@hXX:	beq	@h12
	cmp	#10
	bpl	@h1X
@h0X:	tax
	lda	z_rom_7s_0,x
	sta	z_out_7s_1
	rts
@h12:	lda	#12
@h1X:	sec
	sbc	#10
	tax
	lda	z_rom_7s_0,x
	sta	z_out_7s_1
	lda	z_rom_7s_1
	sta	z_out_7s_0
	rts

c_uv_min1:
	ldx	z_mdl_m1
	lda	z_rom_7s_0,x
	sta	z_out_7s_2
	rts

c_uv_min0:
	ldx	z_mdl_m0
	lda	z_rom_7s_0,x
	sta	z_out_7s_3
	rts


.endif

.if (level = 8)

.export c_main
.export c_handler
.export c_halstart
.export c_upd_view
.export d_tab_f_lo
.export d_tab_f_hi
.export d_tab_sttr
.export z_irq_lo
.export z_irq_hi
.export z_wdtmr
.export z_status
.export z_in_keys
.export z_offset
.export z_tmp_f_lo
.export z_tmp_f_hi

c_main:
	lda	#&lt;c_handler
	sta	z_irq_lo
	lda	#&gt;c_handler
	sta	z_irq_hi
	jmp	c_halstart

c_no_func:
	rts

c_handler:

	lda	#100
	sta	z_wdtmr

	clc
	lda	z_status
	adc	z_status
	adc	z_status
	adc	z_in_keys
	sta	z_offset

	ldx	z_offset
	lda	d_tab_f_lo,x
	sta	z_tmp_f_lo
	lda	d_tab_f_hi,x
	sta	z_tmp_f_hi
	jsr	call_func

	ldx	z_offset
	lda	d_tab_sttr,x
	sta	z_status

	jmp	c_upd_view

call_func:
	jmp	(z_tmp_f_lo)

.endif

.if (level &gt;= 7)
	.res	1*256, 0
.elseif (level &gt;= 5)
	.res	2*256, 0
.else
	.res	3*256, 0
.endif

.data

.if (level &gt;= 1)

vleds_table:
vleds_table_am:
	.byte 32, 'A', 33, 'M'
vleds_table_pm:
	.byte 35, 'P', 36, 'M'
vleds_table_dots:
	.byte 14, ':', 24, ' '

.endif

.if (level &gt;= 3)

segments_table:
	.byte	0, ALINE_RIGHT, 1, ALINE_LEFT
	.byte	1, ALINE_BOTTOM, 3, ALINE_TOP
	.byte	3, ALINE_BOTTOM, 5, ALINE_TOP
	.byte	4, ALINE_RIGHT, 5, ALINE_LEFT
	.byte	2, ALINE_BOTTOM, 4, ALINE_TOP
	.byte	0, ALINE_BOTTOM, 2, ALINE_TOP
	.byte	2, ALINE_RIGHT, 3, ALINE_LEFT

d_tab_av_d:
	.byte	40, 41, 50, 51, 60, 61

.endif

.if (level &gt;= 4)

d_regimage:
	.byte	0		; out_7s_0
	.byte	0		; out_7s_1
	.byte	0		; out_7s_2
	.byte	0		; out_7s_3
	.byte	0		; out_leds
	.byte	0		; in_keys
	.byte	0		; status
	.byte	100		; wdtmr
	.byte	%11111100	; mask_7s0
	.byte	%01100000
	.byte	%11011010
	.byte	%11110010
	.byte	%01100110
	.byte	%10110110
	.byte	%10111110
	.byte	%11100000
	.byte	%11111110
	.byte	%11110110
	.byte	1		; rom_vers
	.byte	10		; rom_freq

d_errormsg:
	.byte	&quot;INT. ERROR&quot;, 0

.endif

.if (level &gt;= 5)

d_dsp_dgts:
	.byte	0, 2, 5, 7

.endif

.if (level &gt;= 7)

d_tab_efld:
	.byte	z_mdl_hh, z_mdl_m1, z_mdl_m0
d_tab_emax:
	.byte	24, 6, 10

.endif

.if (level = 8)

.export v_st_sethh
.export v_st_setm1
.export v_st_setm0
.export v_st_hhmm
.export v_st_ss

.export c_no_func
.export c_mdl_init
.export c_mdl_edit
.export c_mdl_tick
.export c_mdl_go

d_tab_f_lo:
	;	0		SET		MODE
	.byte	&lt;c_mdl_init,	&lt;c_mdl_init,	&lt;c_mdl_init	; init
	.byte	&lt;c_no_func,	&lt;c_no_func,	&lt;c_mdl_edit	; seth
	.byte	&lt;c_no_func,	&lt;c_no_func,	&lt;c_mdl_edit	; setm1
	.byte	&lt;c_no_func,	&lt;c_mdl_go,	&lt;c_mdl_edit	; setm0
	.byte	&lt;c_mdl_tick,	&lt;c_no_func,	&lt;c_mdl_tick	; hhmm
	.byte	&lt;c_mdl_tick,	&lt;c_no_func,	&lt;c_mdl_tick	; ss

d_tab_f_hi:
	;	0		SET		MODE
	.byte	&gt;c_mdl_init,	&gt;c_mdl_init,	&gt;c_mdl_init	; init
	.byte	&gt;c_no_func,	&gt;c_no_func,	&gt;c_mdl_edit	; seth
	.byte	&gt;c_no_func,	&gt;c_no_func,	&gt;c_mdl_edit	; setm1
	.byte	&gt;c_no_func,	&gt;c_mdl_go,	&gt;c_mdl_edit	; setm0
	.byte	&gt;c_mdl_tick,	&gt;c_no_func,	&gt;c_mdl_tick	; hhmm
	.byte	&gt;c_mdl_tick,	&gt;c_no_func,	&gt;c_mdl_tick	; ss

d_tab_sttr:
	;	0		SET		MODE
	.byte	v_st_sethh,	v_st_sethh,	v_st_sethh	; init
	.byte	v_st_sethh,	v_st_setm1,	v_st_sethh	; seth
	.byte	v_st_setm1,	v_st_setm0,	v_st_setm1	; setm1
	.byte	v_st_setm0,	v_st_hhmm,	v_st_setm0	; setm0
	.byte	v_st_hhmm,	v_st_sethh,	v_st_ss		; hhmm
	.byte	v_st_ss,	v_st_sethh,	v_st_hhmm	; ss

.endif

	.res	1*256, 0

</pre>

</body></html>
